apply plugin: 'com.android.library'

ext {
    adapterVersion = '4.3.48'
}

def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

repositories {
    google()
    maven {
        url 'https://android-sdk.is.com/'
    }

}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 30
        versionCode 1
        versionName adapterVersion
        buildConfigField "String", "GitHash", "\"${getGitHash()}\""
        buildConfigField "String", "VERSION_NAME", "\"${defaultConfig.versionName}\""
    }
    buildTypes {
        debug{
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude '**/res.values.values.xml'
    }
}

 dependencies {
     implementation 'com.ironsource.sdk:mediationsdk:8.3.0'
     implementation 'com.facebook.android:audience-network-sdk:6.18.0'
     implementation 'com.facebook.infer.annotation:infer-annotation:0.18.0'
     implementation 'org.jetbrains:annotations:16.0.2'
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from android.sourceSets.main.java.srcDirs
    exclude 'res.values.values.xml'
}

task createAAR(type: Copy,group:'buildTasks') {

    dependsOn (":${project.name}:assembleRelease")
    from "build/outputs/aar"
    into '../ReleaseCandidates/'
    include '*.aar'
    rename { String fileName ->
        fileName.replace('release', adapterVersion)
    }
}


